# Maintainer: Morgan <morganamilo@archlinux.org>
pkgname=yay.rs-git
_pkgname=yay.rs
pkgver=0.0.1.r8.78e5f2f
pkgrel=1
pkgdesc='Rust port of yay'
url='https://github.com/morganamilo/yay.rs'
source=("git+https://github.com/morganamilo/yay.rs#branch=dev")
arch=('x86_64' 'i686')
license=('GPL3')

makedepends=('cargo')
depends=('git' 'pacman' 'vifm')
conflicts=('yay')

options+=(!strip)

sha256sums=(SKIP)

build () {
  cd "$srcdir/$_pkgname"

  if pacman -T pacman-git > /dev/null; then
    _features+="git,generate,"
  fi

  if [[ $(rustc -V) == *"nightly"* ]]; then
    _features+="backtrace,"
  fi

  YAY_VERSION=$pkgver cargo build --features "${_features:-}" --release
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm755 target/release/yay "${pkgdir}/usr/bin/yay"
  install -Dm644 yay.conf "${pkgdir}/etc/yay.conf"
}

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "0.0.1.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

